var i, k, lavaColour, lavaSurfaceLength, type;

type=global.theArgument0
lavaColour=c_yellow
lavaSurfaceLength=200/2

if (type == PRELOAD){
    shadowLength=0.1
    sprPlatform=sprite_add(global.homeDir+"\Stages\PlanetZebes\Platform.gif",1,0,1,0,0,32,10)
    sprite_set_bbox_mode(sprPlatform,2)
    sprite_set_bbox(sprPlatform,0,1,64,20)

    i=instance_create(780, 255+random(500-255), objJumpBlock)
    i.sprite_index=sprPlatform
    i.ySpeed=1
    i.ySticky=1
    i.minY=250
    i.maxY=500
    i.goDown=choose(1,0)
    i.visible=true

    bottomLava=true
    lavaDmg=20
    lavaDmgRandom=10
    lavaVAttackSpeed=6
    lavaVAttackSpeedRandom=1.75

}else if (type == STEP) && (global.roundStart){
    if (theCurrentBackground=MORNING)    { k=3 }
    else if (theCurrentBackground=DAY)   { k=4 }
    else if (theCurrentBackground=SUNSET){ k=6 }
    else if (theCurrentBackground=NIGHT) { k=5 }

    if (!variable_local_exists('maxLavaY')){
        maxLavaY=370//545-random(545-445) //height of lava
        lavaVSpeed=random(6)+2 //speed lava moves at
        lavaTimer=random(400)  //amount of ticks lava stays at point
        lavaUp=true
    }

    if (lavaTimer <= 0){
        if (lavaUp){
            if (lavaY - lavaVSpeed >= maxLavaY){
                lavaY -= lavaVSpeed
            }else{//reached high point
                lavaTimer=random(300)+100
                lavaVSpeed=random(k)+0.5
                maxLavaY=500+random(room_height+100-500)//low point (high number)
                lavaUp=false
            }
        }else if (!lavaUp){
            if (lavaY + lavaVSpeed <= maxLavaY){
                lavaY += lavaVSpeed
            }else{//reached low point
                lavaTimer=random(300)+100
                lavaVSpeed=random(k)+0.5
                maxLavaY=max(256,500-random(545-256))//high point (low number)
                lavaUp=true
            }
        }
    }else{
        lavaTimer-=1
    }

}else if (type == DRAW) && (global.roundStart){
    if (variable_local_exists('lavaY')){
        draw_set_alpha(0.5)
        draw_set_color(lavaColour)
        draw_rectangle(0,lavaY+lavaSurfaceLength,room_width,room_height,0)//bottom part
        draw_set_color(lavaColour-50)
        draw_rectangle(0,objGUI.lavaY,room_width,lavaY+lavaSurfaceLength,0)//surface part above platforms
    }

}else if (type == DRAWBEHIND) && (global.roundStart){
    if (variable_local_exists('lavaY')){
        draw_set_alpha(0.5)
        draw_set_color(lavaColour-50)
        draw_rectangle(0,objGUI.lavaY-lavaSurfaceLength,room_width,lavaY,0)//surface part behind platforms
    }


}else if (type == UNLOAD){
    if (sprite_exists(sprPlatform)){ sprite_delete(sprPlatform) }
}