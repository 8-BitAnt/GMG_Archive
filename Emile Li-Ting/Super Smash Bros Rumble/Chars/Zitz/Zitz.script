var charAction,i,j;

charAction=global.theArgument0;

if (charAction == HASBEENHIT){ exit
}else if (charAction == HASBEENGRABBED){ exit
}else if (eightBitTimer > 0) && (charAction >= ATTACK) && (charAction <= SPECIALSMASHDOWN){
    SetAttackBoxParameters(4,5);
    movementModifier=1
    exit
}

switch(charAction)
{
case ATTACK:
    if (comboCount>2){
        comboCount=0
        i=choose(0,0,1,1,2,2,3,4,5);
        if (i == 0){
            ActionScript(global.character[pNum], SMASHSIDE);
        }else if (i == 1){
            ActionScript(global.character[pNum], SMASHUP);
        }else if (i == 2){
            ActionScript(global.character[pNum], SMASHDOWN);
        }else if (i == 3){
            ActionScript(global.character[pNum], SPECIAL);
        }else if (i == 4){
            ActionScript(global.character[pNum], SPECIALUP);
        }else if (i == 5){
            ActionScript(global.character[pNum], SPECIALDOWN);
        }
    }else{
        SetAttackBoxParameters(4,5);
//        theAttack.image_speed=0.8//1
        theAttack.hSpeedSet=true
        theAttack.hSpeedMinReq=false
        theAttack.vAttackSpeed=1.5
        theAttack.hAttackSpeed=0.6
        
        if (comboCount<=0){
            theAttack.multiHSpeedFrame[0]=4
            theAttack.multiHSpeed[0]=2
            theAttack.comboCount=1
        }else if (comboCount > 0) && (comboCount <= 1){
            theAttack.sprite_index=sprAttack2
            theAttack.attackFrameStart=2
            theAttack.attackFrameEnd=4
            theAttack.multiHSpeedFrame[0]=2
            theAttack.multiHSpeed[0]=2
            theAttack.comboCount=2
        }else if (comboCount > 1){
            theAttack.sprite_index=sprAttack3
            theAttack.attackFrameStart=1
            theAttack.attackFrameEnd=1
            theAttack.multiHSpeedFrame[0]=1
            theAttack.multiHSpeed[0]=1
            theAttack.comboCount=3
        }
        theAttack.cooldownTimer=0
    }
    break
case ATTACKSIDE: ActionScript(global.character[pNum], ATTACK); break
case ATTACKUP: ActionScript(global.character[pNum], ATTACK); break
case ATTACKDOWN:
    i=choose(0,1);
    if (i==0){
        SetAttackBoxParameters(3,4)
        theAttack.multiVSpeedFrame[0]=1
        theAttack.multiVSpeed[0]=-2
        theAttack.multiHSpeedFrame[0]=2
        theAttack.multiHSpeed[0]=2
    }else{
        SetAttackBoxParameters(2,3)
        theAttack.sprite_index=sprAttack4
    }
    break

case RUNATTACK: SetAttackBoxParameters(1,4); break
    
case AIRATTACK:
    SetAttackBoxParameters(3,5)
    theAttack.endOnGround=true
    break
case AIRATTACKSIDE:
    SetAttackBoxParameters(2,3)
    theAttack.endOnGround=true
    break
case AIRATTACKUP:
    SetAttackBoxParameters(3,3)
    theAttack.endOnGround=true
    theAttack.multiHit[0]=2;
    
    theAttack.multiHHitSpeed=0
    theAttack.multiVSpeedMinReq=false
    theAttack.multiVSpeedMin=1
    theAttack.multiVHitSpeed=1
    theAttack.multiAttackPower=5*attackMultiplier
    theAttack.multiStunningPower=8
    break
case AIRATTACKDOWN:
    //frog stomp
    SetAttackBoxParameters(1,2)
    theAttack.elementalType='crush'
    theAttack.multiVSpeedFrame[0]=0
    theAttack.multiVSpeed[0]=5
    theAttack.multiVSpeedFrame[1]=1
    theAttack.multiVSpeed[1]=10
    theAttack.multiVSpeedFrame[2]=2
    theAttack.multiVSpeed[2]=18
    theAttack.hAttackSpeed=5
    theAttack.vAttackSpeed=3
    theAttack.onlyAttackOnVSpeed=true
    theAttack.cooldownTimer=6
    theAttack.endOnGround=true
    theAttack.endOnGroundCooldown=2
    break

case SMASHSIDE:
    SetAttackBoxParameters(4,7);
    theAttack.chargeFrameStart=3
    theAttack.chargeFrameEnd=3
    theAttack.multiHSpeedFrame[0]=0
    theAttack.multiHSpeed[0]=0
    theAttack.whiffFrame=4
    break
case SMASHUP:
    SetAttackBoxParameters(2,3);
    theAttack.chargeFrameStart=0
    theAttack.chargeFrameEnd=0
    theAttack.whiffFrame=1
    break
case SMASHDOWN:
    SetAttackBoxParameters(1,3);
    theAttack.vAttackSpeed=-6
    theAttack.chargeFrameStart=0
    theAttack.chargeFrameEnd=0
    theAttack.whiffFrame=1
    break

case SPECIAL:
    if (specialState != INSTANTPRESSED){ break }
    if (finalSmash){
        //Big Bulldozer
        hspeed=0
        vspeed=0
        suspendGravity=true
        suspendFriction=true
        friction=0
        gravity=0.15
        SetAttackBoxParameters(1,4);
        
        theAttack.sprite_index=sprSpecialSide
        theAttack.image_speed=0.35
        theAttack.image_xscale=abs(image_xscale)*2
        theAttack.image_yscale=(image_yscale)*2
        theAttack.attackPower=40*attackMultiplier
        theAttack.stunningPower=25
        theAttack.hAttackSpeed=12
        theAttack.vAttackSpeed=4
        theAttack.copyScale=false

        theAttack.multiHSpeedFrame[0]=0
        theAttack.multiHSpeed[0]=0
        theAttack.multiHSpeedFrame[1]=1
        theAttack.multiHSpeed[1]=13
        theAttack.multiHSpeedFrame[2]=4
        theAttack.multiHSpeed[2]=5
        theAttack.multiVSpeedFrame[0]=1
        theAttack.multiVSpeed[0]=-3
        
        theAttack.finalSmash=true
        theAttack.endSuspendGravity=true
        exit
    }
    
    //cymbal crash
    SetAttackBoxParameters(3,6);
    theAttack.sprite_index=sprSpecial
    theAttack.hAttackSpeed=4
    theAttack.vAttackSpeed=2
    theAttack.multiHSpeedFrame[0]=3
    theAttack.multiHSpeed[0]=3
    theAttack.stunningPower=20
    break
case SPECIALSIDE:
    if (specialState != INSTANTPRESSED){ break }
    //bulldozer
    SetAttackBoxParameters(1,4);
    theAttack.hAttackSpeed=3
    theAttack.vAttackSpeed=2
    theAttack.multiHSpeedFrame[0]=0
    theAttack.multiHSpeed[0]=0
    theAttack.multiHSpeedFrame[1]=1
    theAttack.multiHSpeed[1]=9
    theAttack.multiHSpeedFrame[2]=4
    theAttack.multiHSpeed[2]=2
    theAttack.stunningPower=12
    break
case SPECIALUP:
    //????
    if (!jumpCheck(true)){ exit }
    
    SetAttackBoxParameters(2,4);
    theAttack.attackPower=10*attackMultiplier
    theAttack.stunningPower=20
    theAttack.hAttackSpeed=3
    theAttack.vAttackSpeed=6
    theAttack.multiHSpeedFrame[0]=2
    theAttack.multiVSpeedFrame[0]=3
    theAttack.absoluteImageXscale=true
    if (onGround){
        theAttack.multiHSpeed[0]=4
        theAttack.multiVSpeed[0]=-7
        theAttack.multiVSpeedFrame[1]=10
        theAttack.multiVSpeed[1]=-4
    }else{
        movementModifier=1
        theAttack.multiHSpeed[0]=2
        theAttack.multiVSpeed[0]=-12
    }
    break
case SPECIALDOWN:
    i=choose(1,2,3) //random special
    if (i == 1)     { ActionScript(global.character[pNum], SPECIAL) }
    else if (i == 2){ ActionScript(global.character[pNum], SPECIALUP) }
    else if (i == 3){ ActionScript(global.character[pNum], SPECIALSIDE) }
    break
case AIRSPECIAL: ActionScript(global.character[pNum], SPECIAL); break
case AIRSPECIALSIDE: ActionScript(global.character[pNum], SPECIALSIDE); break
case AIRSPECIALUP: ActionScript(global.character[pNum], SPECIALUP); break
case AIRSPECIALDOWN: ActionScript(global.character[pNum], SPECIALDOWN); break
    
case SPECIALSMASHSIDE: ActionScript(global.character[pNum], SPECIALSIDE); break
case SPECIALSMASHUP: ActionScript(global.character[pNum], SPECIALUP); break
case SPECIALSMASHDOWN: ActionScript(global.character[pNum], SPECIALDOWN); break

case GRAB:
    SetAttackBoxParameters(2,3)
//    theAttack.image_speed=0.3//0.25
    theAttack.holdDistanceX=1
    theAttack.holdDistanceY=0.5
    break
case THROWATTACK:
case THROWSPECIAL:
    //should be attack, then slam and throw?
    SetAttackBoxParameters(1,1)
    if (comboCount>3){
        theAttack.sprite_index=sprSmashUp
        theAttack.image_speed=0.3
        theAttack.attackPower=(3+random(3))*attackMultiplier
        theAttack.attackFrameStart=2
        theAttack.attackFrameEnd=3
        theAttack.absoluteDirectionHit=true
        theAttack.stunningPower=10
        
        theAttack.hAttackSpeed=3
        theAttack.hSpeedMin=12
        theAttack.vAttackSpeed=4
        theAttack.vSpeedMin=7
        theAttack.cooldownTimer=6
        theAttack.comboCount=0
        exit
    }
    theAttack.sprite_index=getSprite(alias+'ThrowAttack')
    theAttack.image_speed=0.5+comboCount/3
    theAttack.attackPower=(1+random(1))*attackMultiplier
    theAttack.isAThrow=false
    theAttack.comboCount=comboCount+1
    break
case THROWBACK:
    SetAttackBoxParameters(4,5)
    theAttack.image_speed=0.25
    theAttack.absoluteDirectionHit=true
    theAttack.stunningPower=10
    theAttack.hAttackSpeed=-4
    theAttack.hSpeedMin=-10
    theAttack.vAttackSpeed=4
    theAttack.vSpeedMin=8
    theAttack.comboCount=0

    theAttack.holdDistanceFrame[0]=2
    theAttack.holdDistanceFrame[1]=3
    theAttack.holdDistanceFrame[2]=4
    theAttack.holdDistanceFrame[3]=INVALID
    theAttack.holdDistanceX[0]=0.4
    theAttack.holdDistanceX[1]=-0.4
    theAttack.holdDistanceX[2]=-1
    theAttack.holdDistanceY[0]=0.5
    theAttack.holdDistanceY[1]=0.5
    theAttack.holdDistanceY[2]=0.5
    break
case THROWUP:
    SetAttackBoxParameters(2,2)
//    theAttack.image_speed=0.4
    theAttack.absoluteDirectionHit=true
    theAttack.stunningPower=10
    theAttack.hAttackSpeed=0
    theAttack.vAttackSpeed=6
    theAttack.vSpeedMin=18
    theAttack.cooldownTimer=6
    theAttack.comboCount=0

    theAttack.holdDistanceFrame[0]=0
    theAttack.holdDistanceFrame[1]=1
    theAttack.holdDistanceFrame[2]=2
    theAttack.holdDistanceFrame[3]=INVALID
    theAttack.holdDistanceX[0]=1
    theAttack.holdDistanceX[1]=0.8
    theAttack.holdDistanceX[2]=-0.2
    theAttack.holdDistanceY[0]=0.4
    theAttack.holdDistanceY[1]=0.5
    theAttack.holdDistanceY[2]=1
    break
case ROLL:
case AIRDODGE:
    SetAttackBoxParameters()
    break

case INITCHAR:
    sprXScale=0.7
    sprYScale=0.7
    gender='m'
    moveSpeed=MOVESPEED*0.85//0.85
    maxRunSpeed=MAXRUNSPEED*0.87//0.875
    maxAirSpeed=MAXAIRSPEED*0.97//0.975
    jumpSpeed=JUMPSPEED*1.4//1.35
    airJumpSpeed=AIRJUMPSPEED*0.95//0.9
    jumpGravity=JUMPGRAVITY*1.25
    fallGravity=FALLGRAVITY*1.25//1.2
    airCrouchSpeed=AIRCROUCHSPEED*1.3//1.25
    airSlowFallSpeed=AIRSLOWFALLSPEED*1.15//1.1
    idleAnimSpeed=0.25
    
    hasCrit=true
    critBonus=CRITBONUS
    hasSmashCrit=true
    smashCritPercentage=SMASHCRITPERCENTAGE
    highSpecialRisker=true
    hurtSpecialMultiplier=HURTSPECIALMULTIPLIER
    
    hDamageSpeedMultiplier=1.2//1.1
    vDamageSpeedMultiplier=1//0.9
    hHitDistanceEquation=HHITDISTANCEEQUATION*1.2//1.1
    vHitDistanceEquation=VHITDISTANCEEQUATION*0.85//0.9
    break
}