var charAction,i,j,k;

charAction=global.theArgument0;

if (charAction == HASBEENHIT){ exit
}else if (charAction == HASBEENGRABBED){ exit
}else if (eightBitTimer > 0) && (charAction >= ATTACK) && (charAction <= SPECIALSMASHDOWN){
    SetAttackBoxParameters();
    movementModifier=1
    exit
}

switch(charAction)
{
case ATTACK: SetAttackBoxParameters(1,2); break
case ATTACKSIDE: ActionScript(global.character[pNum], ATTACK); break
case ATTACKUP:
    SetAttackBoxParameters(4,7);
    movementModifier=1
    theAttack.hAttackSpeed=0
    theAttack.vAttackSpeed=5
    break
case ATTACKDOWN: SetAttackBoxParameters(1,2); break
case RUNATTACK: SetAttackBoxParameters(0,2); break
    
case AIRATTACK:
    SetAttackBoxParameters(2,3);
    theAttack.endOnGround=true
    break
case AIRATTACKSIDE: ActionScript(global.character[pNum], AIRATTACK); break
case AIRATTACKUP:
    SetAttackBoxParameters(3,5);
    movementModifier=0
    theAttack.endOnGround=true
    break
case AIRATTACKDOWN:
    SetAttackBoxParameters(6,6);
    movementModifier=0
    theAttack.endOnGround=true
    
    theAttack.multiHit[0]=0
    theAttack.multiHit[1]=2
    theAttack.multiHit[2]=4
    theAttack.multiHSpeedMinReq=false
    theAttack.multiVSpeedMinReq=false
    theAttack.multiHDrawIn=true
    theAttack.multiVHitSpeed=-1
    theAttack.multiAttackPower=1.5*attackMultiplier
    theAttack.multiStunningPower=8
    
    theAttack.attackPower=2*attackMultiplier
    theAttack.hAttackSpeed=5
    theAttack.vAttackSpeed=1
    break

case SMASHSIDE:
    SetAttackBoxParameters(7,9);
    theAttack.multiHSpeedFrame[0]=4
    theAttack.multiHSpeed[0]=5
    theAttack.hAttackSpeed=6
    theAttack.vAttackSpeed=3
    theAttack.chargeFrameStart=3
    theAttack.chargeFrameEnd=3
    theAttack.whiffFrame=4
    theAttack.whiffSound2=choose (soundWhiff, soundWhiff2, soundWhiff3)
    sound_volume2(theAttack.whiffSound2,0.9)
    theAttack.whiffFrame2=4
    break
case SMASHUP:
    SetAttackBoxParameters(2,4);
    theAttack.hAttackSpeed=0
    theAttack.vAttackSpeed=7
    theAttack.multiVSpeedFrame[0]=3
    theAttack.multiVSpeed[0]=-3
    theAttack.chargeFrameStart=1
    theAttack.chargeFrameEnd=1
    theAttack.whiffFrame=2
    theAttack.whiffSound2=choose (soundWhiff, soundWhiff2, soundWhiff3)
    sound_volume2(theAttack.whiffSound2,0.9)
    theAttack.whiffFrame2=2
    break
case SMASHDOWN:
    SetAttackBoxParameters(4,9);
    theAttack.hAttackSpeed=4
    theAttack.vAttackSpeed=1
    theAttack.chargeFrameStart=3
    theAttack.chargeFrameEnd=3
    theAttack.whiffFrame=4
    theAttack.whiffSound2=choose (soundWhiff, soundWhiff2, soundWhiff3)
    sound_volume2(theAttack.whiffSound2,0.9)
    theAttack.whiffFrame2=4
    break
    
case SPECIAL:
    if (specialState != INSTANTPRESSED){ break }
    if (finalSmash){
        //vacuum deluxe
        hspeed=0
        vspeed=0
        suspendGravity=true
        suspendFriction=true
        friction=0
        gravity=0
        SetAttackBoxParameters(-1,-1);
        theAttack.sprite_index=sprSpecial2
        theAttack.finalSmash=true
        theAttack.endSuspendGravity=true
        
        i= instance_create(x,y,AttackBox);   
        i.attackPower=15*attackMultiplier
        i.stunningPower=20
        i.hAttackSpeed=10
        i.hSpeedMin=25+random(5)
        i.vAttackSpeed=3
        i.attackFrameStart=0
        i.attackFrameEnd=15
        i.hitAll=true
        i.ignoreGuard=true
        i.absoluteImageXscale=true//<<<<<REQUIRED????????
        i.pNum=pNum
    
        j = instance_create((view_wport[0] - view_xport[0])/2, (view_hport[0] - view_yport[0])/2, ImageEffect);
        j.sprite_index=sprSpecial2
        j.setProjection=true
        j.image_xscale=5
        j.image_yscale=j.image_xscale
        j.fadeOut=true
        j.grow=true
        j.deathTimerMax=35
    
        exit
    }

    //fireball    
    SetAttackBoxParameters(-1,-1);    
    k=0 //projectileCountNum
    if (projectileCount[k] > 1){
        exit
    }
    projectileCount[k]+=1
    
    theAttack.whiffSound=soundSpecial
    theAttack.whiffFrame=2
    
    i=(bbox_right-bbox_left)/3
    j=(bbox_bottom-bbox_top)/2
    SetProjectileParameters(i,j)
    if (random(4)<1){ theProjectile.elementalType='fire' }
    theProjectile.deathTimer=35
    theProjectile.attackPower=5*attackMultiplier
    theProjectile.stunningPower=4
    theProjectile.hAttackSpeed=1.5
    theProjectile.vAttackSpeed=1
    theProjectile.smokeSprite=SamusSmokeTrail
    theProjectile.smokeXScale=1.2
    theProjectile.smokeYScale=theProjectile.smokeXScale
    theProjectile.smokeXYVariance=2
    theProjectile.smokeSizeVariance=0.3
    theProjectile.hSpeedMin=false
    theProjectile.vSpeedMin=false
    theProjectile.alphaDecay=true
    theProjectile.decayOnImpact=true
    theProjectile.projectileCountNum=k
    theProjectile.appearanceFrame=2
    theProjectile.theHSpeed=8
    theProjectile.hitSound=soundProjectile
    break
case SPECIALSIDE:
    vspeed=-1.5
    suspendGravity=true
    gravity=0.15//0.1
    suspendFriction=true
    friction=0
    
    i=choose(5,5,5,5,5,5,5,10)
    if (image_xscale > 0){
        if (hspeed<0){
            hspeed=i
        }else{
            hspeed+=i
        }
    }else if (image_xscale < 0){
        if (hspeed>0){
            hspeed=-i
        }else{
            hspeed-=i
        }
    }
    
    SetAttackBoxParameters(2,9);
    theAttack.endOnGround=true
    theAttack.hAttackSpeed=4
    theAttack.vAttackSpeed=3
    theAttack.cooldownTimer=10
    break
case SPECIALUP:
    if (!jumpCheck()){ exit }

    if (hspeed>1){
        hspeed=1
    }else if (hspeed<-1){
        hspeed=-1
    }
    
    SetAttackBoxParameters(3,6);
    if (!onGround){        
        if (gravity != fallGravity){
            gravity=fallGravity
        }
        movementModifier=0
    }
    theAttack.attackPower=10*attackMultiplier
    theAttack.stunningPower=16
    
    theAttack.multiHit[0]=2;
    theAttack.multiHHitSpeed=0.05
    theAttack.multiVHitSpeed=6 + choose(2,2,3,3,3,4,4)
    theAttack.multiAttackPower=25*attackMultiplier
    theAttack.multiStunningPower=16
    theAttack.multiHitUnique=false
//    theAttack.multiElementalType='fire'//DOES NOT EXIST YET
    
    theAttack.multiVSpeedFrame[0]=2
    if (onGround){ theAttack.multiVSpeed[0]=-jumpSpeed }
    else{ theAttack.multiVSpeed[0]=-jumpSpeed*0.875}
    
    theAttack.hAttackSpeed=0.05
    theAttack.vAttackSpeed=6 + choose(0,1,1,1,2,2,2)
    theAttack.vSpeedMin=9

    theAttack.whiffSound=soundSpecial2
    theAttack.whiffFrame=1
    sound_volume2(theAttack.whiffSound,0.9)
    theAttack.hitSound=soundSpecial3
    sound_volume2(theAttack.hitSound,0.9)
    break
case SPECIALDOWN:
    suspendGravity=true
    suspendFriction=true
    friction=0
    if (onGround){
        gravity=0.45
        vspeed=-6
    }else{
        gravity=0.5//0.65
        if (vspeed > 0){ //falling
            vspeed=1
        }else{
            vspeed=-2
        }
    }
    
    SetAttackBoxParameters(7,8);
    
    theAttack.multiHit[0]=1
    theAttack.multiHit[1]=3
    theAttack.multiHit[2]=5
    theAttack.multiHSpeedMinReq=false
    theAttack.multiVSpeedMinReq=false
    theAttack.multiHDrawIn=true
    theAttack.multiVHitSpeed=-1
    theAttack.multiAttackPower=4*attackMultiplier
    theAttack.multiStunningPower=8

    theAttack.attackPower=4*attackMultiplier
    theAttack.absoluteImageXscale=true
    theAttack.hAttackSpeed=4
    if (onGround){
        theAttack.vAttackSpeed=4
    }else{
        theAttack.vAttackSpeed=-4
    }
    theAttack.postAttackVSpeed=-3
    theAttack.endSuspendGravity=true
    theAttack.endSuspendFriction=true
    theAttack.cooldownTimer=8
    break
       
case AIRSPECIAL: ActionScript(global.character[pNum], SPECIAL); break
case AIRSPECIALSIDE: ActionScript(global.character[pNum], SPECIALSIDE); break
case AIRSPECIALUP: ActionScript(global.character[pNum], SPECIALUP); break
case AIRSPECIALDOWN: ActionScript(global.character[pNum], SPECIALDOWN); break
    
case SPECIALSMASHSIDE: ActionScript(global.character[pNum], SPECIALSIDE); break
case SPECIALSMASHUP: ActionScript(global.character[pNum], SPECIALUP); break
case SPECIALSMASHDOWN: ActionScript(global.character[pNum], SPECIALDOWN); break

case ROLL:
case AIRDODGE: SetAttackBoxParameters(); break
case INITCHAR:
    gender='m'
    walkAnimSpeed=0.75
    runAnimSpeed=0.8
    floorFriction=FLOORFRICTION*0.9
    turnFriction=TURNFRICTION*0.975
    maxRunSpeed=MAXRUNSPEED*0.85
    airJumpAnimSpeed=1
    jumpSpeed=JUMPSPEED*1.15
    maxFallSpeed=MAXFALLSPEED-0.15
    fallGravity=FALLGRAVITY*9/10
    airSlowFallSpeed=AIRSLOWFALLSPEED*0.3
    hDamageSpeedMultiplier=1.01
    vDamageSpeedMultiplier=1.01
    
    guardFrameStart=1
    guardFrameEnd=5

    soundDies=      'Luigi Death.wav'
    soundIsHit=     'Luigi Hit.wav'
    soundIsHit2=    'Luigi Hit2.wav'
    soundIsHit3=    'Luigi Hit3.wav'
    soundIsHit4=    'Luigi Hit4.wav'
    soundWhiff=     'Luigi Attack.wav'
    soundWhiff2=    'Luigi Attack2.wav'
    soundWhiff3=    'Luigi Attack3.wav'
    soundSpecial=   'Mario Fire.wav'
    soundProjectile='Mario Fireball Hit.wav'
    soundSpecial2=  'Super Jump.wav'
    soundSpecial3=  'Smash Ping.wav'
    break
}