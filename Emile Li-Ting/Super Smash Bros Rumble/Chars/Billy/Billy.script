var charAction,i,j,k;

charAction=global.theArgument0;

if (charAction == HASBEENHIT){
    i=global.theArgument1
    if (!global.theArgument2) && (canCounter){
        canCounter=false
        with (theAttack){ instance_destroy() }

        SetAttackBoxParameters(7,9);
        theAttack.sprite_index=sprSpecialDown
        theAttack.image_index=6
        theAttack.image_speed=0.6
        theAttack.attackPower=max(i*2,12)*attackMultiplier//returns double the damage taken (min 12)
        theAttack.stunningPower=10
        theAttack.hAttackSpeed=6
        theAttack.vAttackSpeed=3
        theAttack.invulnerableFrameStart=6
        theAttack.invulnerableFrameEnd=7
        theAttack.hitSound=choose(soundAttack2,soundAttack3)
//        theAttack.whiffSound2=soundSmash
//        theAttack.whiffFrame2=0
    }
}else if (charAction == HASBEENGRABBED){ exit
}else if (charAction >= ATTACK) && (charAction <= SPECIALSMASHDOWN){
    if (eightBitTimer > 0){
        SetAttackBoxParameters();
        movementModifier=1
        exit
    }
}

switch(charAction)
{
case ATTACK:
    if (comboCount > 1){
        comboCount=0
        ActionScript(global.character[pNum], SMASHUP);
    }else{
        SetAttackBoxParameters(2,3);
        
        theAttack.hAttackSpeed=0
        theAttack.vAttackSpeed=0
    
        if (comboCount<=0){
            theAttack.sprite_index=sprAttack
            theAttack.comboCount=1
        }else if (comboCount > 0) && (comboCount <= 1){
            theAttack.sprite_index=sprAttack2
            theAttack.comboCount=2
        }
        theAttack.attackPower=5.5*attackMultiplier
        theAttack.image_speed=0.7
        theAttack.hitSound=choose(soundAttack,soundAttack2)
    }
    break
    
case ATTACKSIDE: ActionScript(global.character[pNum], ATTACK); break
        
case ATTACKUP:
    SetAttackBoxParameters(4,5)
    movementModifier=1
    theAttack.attackPower=5.5*attackMultiplier
    theAttack.hitSound=choose(soundAttack,soundAttack2)
    break
    
case ATTACKDOWN:
    if (comboCount > 3){
        comboCount=0
        ActionScript(global.character[pNum], SMASHSIDE);
    }else{
        SetAttackBoxParameters(2,3);
        movementModifier=1
        theAttack.attackPower=5.5*attackMultiplier
        theAttack.hAttackSpeed=0
        theAttack.vAttackSpeed=0.5
        
        if (comboCount<=0){
            theAttack.sprite_index=sprAttackDown
            theAttack.comboCount=1
        }else if (comboCount <= 1){
            theAttack.sprite_index=sprAttack3
            theAttack.attackFrameStart=3
            theAttack.attackFrameEnd=4
            theAttack.comboCount=2
        }else if (comboCount <= 2){
            theAttack.sprite_index=sprAttack3
            theAttack.hAttackSpeed=4
            theAttack.vAttackSpeed=4
            theAttack.comboCount=3
        }else if (comboCount <= 3){
            theAttack.sprite_index=sprAttack4
            theAttack.hAttackSpeed=4
            theAttack.vAttackSpeed=5
            theAttack.comboCount=4
        }
    
        theAttack.image_speed=0.7
        theAttack.hitSound=choose(soundAttack,soundAttack2)
    }
    break
    
case RUNATTACK:
    SetAttackBoxParameters(0,2);
    theAttack.attackPower=6.5*attackMultiplier
    theAttack.hitSound=choose(soundAttack2,soundAttack3)
    break
    
case AIRATTACK:
    SetAttackBoxParameters(3,4);
    theAttack.hAttackSpeed=4
    theAttack.vAttackSpeed=1
    theAttack.endOnGround=true
    theAttack.hitSound=choose(soundAttack,soundAttack2)
    break
    
case AIRATTACKSIDE: ActionScript(global.character[pNum], AIRATTACK); break
    
case AIRATTACKUP:
    gravity=0
    vspeed=-5    
    SetAttackBoxParameters(4,5);
    movementModifier=0
    theAttack.attackPower=5.5*attackMultiplier
    theAttack.hAttackSpeed=0.5
    theAttack.vAttackSpeed=6.5
    theAttack.endOnGround=true
    theAttack.hitSound=choose(soundAttack,soundAttack2)
    break

case AIRATTACKDOWN:
    if (image_xscale>0){
        hspeed+=4
    }else if (image_xscale<0){
        hspeed-=4
    }
    vspeed=4
    
    SetAttackBoxParameters(3,8);
    theAttack.attackPower=5.5*attackMultiplier
    theAttack.hAttackSpeed=4.5
    theAttack.vAttackSpeed=3
    theAttack.endOnGround=true
    theAttack.hitSound=choose(soundAttack,soundAttack2)
    break

case SMASHSIDE:
    if (faceRight){
        hspeed=2
    }else if (!faceRight){
        hspeed=-2
    }
    
    SetAttackBoxParameters(5,7);
    theAttack.attackPower=16*attackMultiplier
    theAttack.hAttackSpeed=7.2
    theAttack.vAttackSpeed=5.2
    theAttack.chargeFrameStart=2
    theAttack.chargeFrameEnd=2
    theAttack.cooldownTimer=2
    theAttack.whiffFrame=3
    theAttack.whiffSound2=soundSmash
    theAttack.whiffFrame2=3
    theAttack.hitSound=choose(soundAttack2,soundAttack3)
    break
    
case SMASHUP:
    SetAttackBoxParameters(7,9);
    theAttack.attackPower=10*attackMultiplier
    theAttack.hAttackSpeed=0
    theAttack.vAttackSpeed=7.7
    theAttack.chargeFrameStart=3
    theAttack.chargeFrameEnd=3
    theAttack.whiffFrame=4
    theAttack.whiffSound2=soundSmash
    theAttack.whiffFrame2=4
    theAttack.hitSound=choose(soundAttack2,soundAttack3)
    break

case SMASHDOWN:
    SetAttackBoxParameters(4,6);
    theAttack.attackPower=12*attackMultiplier
    theAttack.hAttackSpeed=4.2
    theAttack.vAttackSpeed=1.2
    theAttack.chargeFrameStart=1
    theAttack.chargeFrameEnd=1
    theAttack.whiffFrame=2
    theAttack.whiffSound2=soundSmash
    theAttack.whiffFrame2=2
    theAttack.hitSound=choose(soundAttack2,soundAttack3)
    break
    
case SPECIAL:
    if (specialState == INSTANTPRESSED){
        if (finalSmash){
            hspeed=0
            vspeed=0
            suspendGravity=true
            suspendFriction=true
            friction=0
            gravity=0
            SetAttackBoxParameters(-1,-1);
            theAttack.sprite_index=sprSpecialDown
            theAttack.finalSmash=true
            theAttack.endSuspendGravity=true
        
            i= instance_create(x,y,AttackBox);   
            i.attackPower=45
            i.stunningPower=25
            i.hAttackSpeed=0
            i.vAttackSpeed=8
            i.hitOnGround=true
            i.attackFrameStart=0
            i.attackFrameEnd=15
            i.hitAll=true
            i.ignoreGuard=true
            i.absoluteImageXscale=true
            i.pNum=pNum
    
            j = instance_create((view_wport[0] - view_xport[0])/2, (view_hport[0] - view_yport[0])/2, ImageEffect);
            j.sprite_index=sprSmashDown
            j.setProjection=true
            j.image_xscale=4
            j.image_yscale=j.image_xscale
            j.fadeOut=true
            j.grow=true
            j.deathTimerMax=35
    
            exit
        }

        //throw knife
        SetAttackBoxParameters(-1,-1);
    
        k=0
        if (projectileCount[k] > 15){ //limit until he dies
            theAttack.sprite_index=sprSpecial2 //no knife in throw animation
            exit
        }
        projectileCount[k]+=1
    
        i=(theAttack.bbox_right-theAttack.bbox_left)*0.5
        j=(theAttack.bbox_bottom-theAttack.bbox_top)*0.75    
        SetProjectileParameters(i,j)
        theProjectile.deathTimer=30
        theProjectile.attackPower=12*attackMultiplier
        theProjectile.rotateOnDirection=true
        theProjectile.stunningPower=4
        theProjectile.hAttackSpeed=1.5
        theProjectile.vAttackSpeed=1
        theProjectile.hSpeedMin=false
        theProjectile.vSpeedMin=false
        theProjectile.alphaDecay=true
        theProjectile.decayOnImpact=true
        theProjectile.theSpeed=11
        theProjectile.theDirection=choose(0,-1)*random(6)
        theProjectile.appearanceFrame=3
        theProjectile.hitSound=soundAttack
    }
    break
case SPECIALSIDE: ActionScript(global.character[pNum], SPECIAL); break
    
case SPECIALUP:
    if (!jumpCheck(true)){ exit }
    
    if (image_xscale>0){
        hspeed=5
    }else if (image_xscale<0){
        hspeed=-5
    }
    suspendGravity=true
    vspeed=0
    gravity=0
    suspendFriction=true
    friction=0
    onGround=false
    
    SetAttackBoxParameters(6,18);
    theAttack.image_speed=0.8
    theAttack.attackPower=12.3*attackMultiplier
    theAttack.stunningPower=20
    theAttack.hAttackSpeed=3
    theAttack.vAttackSpeed=2
    theAttack.hitSound=choose(soundAttack2,soundAttack3)
    theAttack.endSuspendGravity=true
    theAttack.endSuspendFriction=true
    break
    
case SPECIALDOWN:
    //counter
    noFlinchTimer=20
    noStunTimer=20
    SetAttackBoxParameters(7,9);
    theAttack.image_speed=0.4
    theAttack.attackPower=9*attackMultiplier
    theAttack.stunningPower=10
    theAttack.hAttackSpeed=6
    theAttack.vAttackSpeed=3
    theAttack.invulnerableFrameStart=0
    theAttack.invulnerableFrameEnd=2
    theAttack.hitSound=choose(soundAttack2,soundAttack3)
    theAttack.whiffSound2=soundSmash
    theAttack.whiffFrame2=0
    theAttack.warmupTimer=6//3
    canCounter=true
    break

case AIRSPECIAL: ActionScript(global.character[pNum], SPECIAL); break
case AIRSPECIALSIDE: ActionScript(global.character[pNum], SPECIALSIDE); break
case AIRSPECIALUP: ActionScript(global.character[pNum], SPECIALUP); break
case AIRSPECIALDOWN: ActionScript(global.character[pNum], SPECIALDOWN); break

case SPECIALSMASHSIDE: ActionScript(global.character[pNum], SPECIALSIDE); break
case SPECIALSMASHUP:
    if (!jumpCheck(true)){ exit }
    
    suspendFriction=true
    friction=0
    onGround=false
    
    SetAttackBoxParameters(6,6);
    theAttack.sprite_index=sprSpecial4
    theAttack.image_speed=0.8
    
    theAttack.multiVSpeedFrame[0]=3
    theAttack.multiVSpeed[0]=-jumpSpeed/2.5
    
    theAttack.attackPower=15*attackMultiplier
    theAttack.stunningPower=20
    theAttack.hAttackSpeed=4
    theAttack.vAttackSpeed=4
    theAttack.cooldownTimer=4
    theAttack.hitSound=choose(soundAttack2,soundAttack3)
    theAttack.endSuspendGravity=true
    theAttack.endSuspendFriction=true
    break
    
case SPECIALSMASHDOWN: ActionScript(global.character[pNum], SPECIALDOWN); break

case GRAB:
    SetAttackBoxParameters(2,3);
    theAttack.image_speed=0.25
    theAttack.holdDistanceX=0.7
    theAttack.holdDistanceY=0.3//0.4
    theAttack.grabTimer=120
    comboCount=0
    break
case THROWATTACK:
    //consistant moderate damage
    SetAttackBoxParameters(4,5);
    theAttack.attackPower=(3+random(2))*attackMultiplier
    theAttack.noSpriteChange=false
    theAttack.isAThrow=false

    if (comboCount<=0.5){
        theAttack.comboCount=comboCount+choose(0.25,0.6,1)
    }else if (comboCount<=1){
        theAttack.sprite_index=getSprite(alias+'ThrowAttack2')
        theAttack.attackFrameStart=3
        theAttack.attackFrameEnd=4
        theAttack.comboCount=2
    }else{
        theAttack.sprite_index=getSprite(alias+'ThrowAttack3')
        theAttack.image_speed=0.4
        theAttack.attackPower=8*attackMultiplier
        theAttack.isAThrow=true
        theAttack.absoluteDirectionHit=true
        theAttack.hAttackSpeed=3
        theAttack.hSpeedMin=7
        theAttack.vAttackSpeed=3
        theAttack.vSpeedMin=6.5
        theAttack.comboCount=0
        theAttack.whiffFrame=4
        theAttack.whiffSound2=soundSmash
        theAttack.whiffFrame2=4
    }
    break
case THROWSPECIAL:
    //erratic low-high damage
    SetAttackBoxParameters(3,4);
    theAttack.attackPower=(1+random(3))*attackMultiplier
    theAttack.noSpriteChange=false
    theAttack.isAThrow=false

    if (comboCount<=0.5){
        theAttack.comboCount=comboCount+choose(0.25,1,1)
    }else if (comboCount<=1){
        theAttack.sprite_index=getSprite(alias+'ThrowSpecial2')
        theAttack.attackFrameStart=2
        theAttack.attackFrameEnd=3
        theAttack.comboCount=2
    }else{
        theAttack.sprite_index=getSprite(alias+'ThrowSpecial3')

        theAttack.multiHit[0]=4;
        theAttack.multiHit[1]=7;
        theAttack.multiHit[2]=9;
        theAttack.multiAttackPower=(1+random(3))*attackMultiplier
        theAttack.multiStunningPower=8

        theAttack.attackFrameStart=11
        theAttack.attackFrameEnd=11
        theAttack.image_speed=0.35
        theAttack.attackPower=1+random(6)*attackMultiplier
        theAttack.isAThrow=true
        theAttack.absoluteDirectionHit=true
        theAttack.hAttackSpeed=4
        theAttack.hSpeedMin=6
        theAttack.vAttackSpeed=5
        theAttack.vSpeedMin=7
        theAttack.comboCount=0
        theAttack.whiffSound2=soundSmash
        theAttack.whiffFrame2=11
    }
    break

case THROWBACK:
    SetAttackBoxParameters(3,3)
    theAttack.attackPower=5*attackMultiplier
    theAttack.image_speed=0.3
    theAttack.absoluteDirectionHit=true
    theAttack.stunningPower=10
    theAttack.hAttackSpeed=-4
    theAttack.hSpeedMin=-10
    theAttack.vAttackSpeed=4
    theAttack.vSpeedMin=8
    theAttack.comboCount=0

    theAttack.holdDistanceFrame[0]=0
    theAttack.holdDistanceFrame[1]=1
    theAttack.holdDistanceFrame[2]=2
    theAttack.holdDistanceFrame[3]=3
    theAttack.holdDistanceFrame[4]=INVALID
    theAttack.holdDistanceX[0]=0.6
    theAttack.holdDistanceX[1]=0.1
    theAttack.holdDistanceX[2]=-0.2
    theAttack.holdDistanceX[3]=-0.5
    theAttack.holdDistanceY[0]=0.3
    theAttack.holdDistanceY[1]=0.4
    theAttack.holdDistanceY[2]=0.5
    theAttack.holdDistanceY[3]=0.6

    if (choose(0,0,1)){
        theAttack.whiffSound2=soundSmash
        theAttack.whiffFrame2=3
    }
    break

case THROWFRONT:
    SetAttackBoxParameters(4,5);
    theAttack.isAThrow=true
    theAttack.absoluteDirectionHit=true
    theAttack.noSpriteChange=false
    if (choose(0,0,1)){
        theAttack.whiffSound2=soundSmash
        theAttack.whiffFrame2=4
    }
    if (comboCount<=1){
        theAttack.sprite_index=getSprite(alias+'ThrowAttack3')
        theAttack.attackPower=(3+random(2))*attackMultiplier
        theAttack.image_speed=0.4
        theAttack.attackPower=8*attackMultiplier
        theAttack.hAttackSpeed=3
        theAttack.hSpeedMin=7
        theAttack.vAttackSpeed=3
        theAttack.vSpeedMin=6.5
    }else{
        theAttack.sprite_index=getSprite(alias+'ThrowSpecial3')
        theAttack.multiHit[0]=4;
        theAttack.multiHit[1]=7;
        theAttack.multiHit[2]=9;
        theAttack.multiAttackPower=(1+random(3))*attackMultiplier
        theAttack.multiStunningPower=8
    
        theAttack.attackFrameStart=11
        theAttack.attackFrameEnd=11
        theAttack.image_speed=0.35
        theAttack.attackPower=1+random(6)*attackMultiplier
        theAttack.hAttackSpeed=4
        theAttack.hSpeedMin=6
        theAttack.vAttackSpeed=5
        theAttack.vSpeedMin=7
        theAttack.whiffFrame2=11
    }
    break
    
case ROLL: SetAttackBoxParameters(8.25); break

case INITCHAR:
    canCounter=false

    sprXScale=0.8
    sprYScale=0.8
    walkAnimSpeed=0.5
    runAnimSpeed=1
    
    gender='m'
    moveSpeed=MOVESPEED*0.75
    maxWalkSpeed=MAXWALKSPEED*1.05
    maxRunSpeed=MAXRUNSPEED*0.825
    airJumpSpeed=AIRJUMPSPEED+1
    jumpSpeed=JUMPSPEED*0.915
    jumpGravity=JUMPGRAVITY*1.0025
    
    hasCrit=true
    critPercentage=CRITPERCENTAGE
    hasSpecialCrit=true
    specialCritPercentage=SPECIALCRITPERCENTAGE
    
    hAttackSpeedMultiplier=1.1
    vAttackSpeedMultiplier=1.1
    hDamageSpeedMultiplier=0.95
    vDamageSpeedMultiplier=0.95
    hHitDistanceEquation=HHITDISTANCEEQUATION*0.98
    vHitDistanceEquation=VHITDISTANCEEQUATION*0.98
    stunEquation=0.6
    
    sprSleepy=getSprite(alias+'Dizzy')

    soundDies=   'Billy Death.wav'
    soundIsHit=  'Billy Hit.wav'
    soundIsHit2= 'Billy Hit2.wav'
    soundAttack= 'Billy Attack1.wav'
    soundAttack2='Billy Attack2.wav'
    soundAttack3='Billy Attack3.wav'
    soundSmash=  'Billy Smash.wav'
    break

case LOSEALIFE: canCounter=false; break
}