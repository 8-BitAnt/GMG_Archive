var charAction,i,j;

charAction=global.theArgument0;

if (charAction == HASBEENHIT){ exit
}else if (charAction == HASBEENGRABBED){ exit
}else if (eightBitTimer > 0) && (charAction >= ATTACK) && (charAction <= SPECIALSMASHDOWN){
    SetAttackBoxParameters();
    movementModifier=1
    exit
}

switch(charAction)
{
case ATTACK:
    SetAttackBoxParameters(3,5);
    if (comboCount<=0){
        theAttack.comboCount=1
    }else if (comboCount > 0){
        if (onGround){
            hspeed=0
        }
        theAttack.sprite_index=sprAttack2
        theAttack.absoluteDirectionHit=true
        theAttack.hAttackSpeed=0.5
        theAttack.hSpeedSet=true
        theAttack.hSpeedMinReq=false 
        theAttack.vAttackSpeed=1.5
        theAttack.attackFrameStart=2
        theAttack.attackFrameEnd=3
        theAttack.comboCount=1
    }
    theAttack.cooldownTimer=0
    break
case ATTACKSIDE: ActionScript(global.character[pNum], ATTACK); break
        
case ATTACKUP:
    SetAttackBoxParameters(3,6);
    movementModifier=1
    theAttack.hAttackSpeed=0
    theAttack.vAttackSpeed=5.5
    break
    
case ATTACKDOWN:
    SetAttackBoxParameters(3,5);
    theAttack.hAttackSpeed=4
    theAttack.vAttackSpeed=1
    break
    
case RUNATTACK: SetAttackBoxParameters(1,3); break
    
case AIRATTACK:
    SetAttackBoxParameters(3,5);
    theAttack.sprite_index=sprAttack//<<<<<<<<REUSED
    theAttack.endOnGround=true
    break
case AIRATTACKSIDE: ActionScript(global.character[pNum], AIRATTACK); break
    
case AIRATTACKUP:
    SetAttackBoxParameters(3,6);
    theAttack.sprite_index=sprAttackUp//<<<<<<<<REUSED
    movementModifier=0
    theAttack.endOnGround=true
    break
    
case AIRATTACKDOWN:
    SetAttackBoxParameters(3,5);
    theAttack.sprite_index=sprAttackDown//<<<<<<<<REUSED
    theAttack.endOnGround=true
    break

case SMASHSIDE:
    SetAttackBoxParameters(4,7);
    theAttack.chargeFrameStart=2
    theAttack.chargeFrameEnd=2
    theAttack.hAttackSpeed=6.75
    theAttack.vAttackSpeed=6.75
    theAttack.cooldownTimer=2
    theAttack.whiffFrame=3
    if (random(4)<1){
        theAttack.whiffSound2=soundWhiff
        theAttack.whiffFrame2=3
    }
    break
    
case SMASHUP:
    SetAttackBoxParameters(3,6);
    theAttack.chargeFrameStart=2
    theAttack.chargeFrameEnd=2
    theAttack.hAttackSpeed=0
    theAttack.vAttackSpeed=6.75
    theAttack.whiffFrame=3
    if (random(4)<1){
        theAttack.whiffSound2=soundWhiff
        theAttack.whiffFrame2=3
    }
    break

case SMASHDOWN:
    SetAttackBoxParameters(3,5);
    theAttack.chargeFrameStart=2
    theAttack.chargeFrameEnd=2
    theAttack.hAttackSpeed=5
    theAttack.vAttackSpeed=5.5
    theAttack.whiffFrame=3
    if (random(4)<1){
        theAttack.whiffSound2=soundWhiff
        theAttack.whiffFrame2=3
    }
    break
    
case SPECIAL:
    if (finalSmash) && (specialState == INSTANTPRESSED){
        hspeed=0
        vspeed=0
        suspendGravity=true
        suspendFriction=true
        friction=0
        gravity=0
        SetAttackBoxParameters(-1,-1);
        theAttack.sprite_index=sprSmashDown
        theAttack.finalSmash=true
        theAttack.endSuspendGravity=true
        
        //play 8 bit sound for getting item
        for (i=0; i<TOTALPLAYERS; i+=1){
            thePlayer=getPlayer(i)
            if (instance_exists(thePlayer)) && (thePlayer != INVALID){
                if (global.livesRemaining[i] >= 0) && (thePlayer.pNum != other.pNum) && (!(isInvulnerable(i))){
                    (thePlayer.eightBitTimer)=500
                    thePlayer.sprite_index = thePlayer.spr8BitIdle
                    //create poof animation on top of player
                }
            }
        }
    
        i = instance_create((view_wport[0] - view_xport[0])/2, (view_hport[0] - view_yport[0])/2, ImageEffect);
        i.sprite_index=spr8BitTimer
        i.theSpeed=0.5
        i.setProjection=true
        i.image_xscale=8
        i.image_yscale=i.image_xscale
        i.fadeOut=true
        i.grow=true
        i.deathTimerMax=35
        
        exit
    }
    //throw bacon
    SetAttackBoxParameters(-1,-1);
    if (random(4)<1){
        theAttack.whiffSound=soundSpecial
        theAttack.whiffFrame=3
        sound_volume2(theAttack.whiffSound,0.9)
    }
    theAttack.image_speed=0.65
    if (onGround){ theAttack.image_speed=0.75 }

    //bacon
    i=(theAttack.bbox_right-theAttack.bbox_left)*0.8
    j=(theAttack.bbox_bottom-theAttack.bbox_top)*0.45
    SetProjectileParameters(i,j)
    theProjectile.image_xscale=abs(image_xscale)*1.25
    theProjectile.image_yscale=theProjectile.image_xscale
    
    theProjectile.theImageSpeed=0.25+random(0.3)
    theProjectile.attackPower=5*attackMultiplier
    theProjectile.stunningPower=7
    theProjectile.hAttackSpeed=1.5
    theProjectile.vAttackSpeed=1
    theProjectile.appearanceFrame=3
    theProjectile.alphaDecay=true
    theProjectile.decayOnImpact=true
    theProjectile.fall=true
    theProjectile.theGravity=1
    theProjectile.theHSpeed=random(4.5)+1
    theProjectile.maxVSpeed=30
    theProjectile.theVSpeed=-(random(4)+9)
    break
    
case SPECIALSIDE: ActionScript(global.character[pNum], SPECIAL); break
    
case SPECIALUP:
    if (specialState != INSTANTPRESSED){ break }
    //Emergency Crew Assist
    if (!jumpCheck()){ exit }
    
    if (!onGround){        
        if (gravity != fallGravity) && (!suspendGravity){
            gravity=fallGravity
        }
    }
    vspeed=-jumpSpeed-3    
    SetAttackBoxParameters(3,5);
    movementModifier=0
    theAttack.sprite_index=sprJump2
    theAttack.stunningPower=18
    theAttack.hAttackSpeed=3
    theAttack.vAttackSpeed=2

    //Emergency Crew
    theEffect = instance_create (x, y, Effect);
    theEffect.sprite_index=sprSpecialUp
    theEffect.image_xscale=image_xscale
    theEffect.image_yscale=image_yscale
    theEffect.freezeOnLastFrame=true
    theEffect.image_speed=0.6
    theEffect.deathTimerMax=15
    break
    
    
case SPECIALDOWN:
    if (specialState != INSTANTPRESSED){ break }//<<<<<<<<<<<<<<<<<<<<<<<<<<<<< should catch projectiles and fill some bar
    SetAttackBoxParameters(4,9);
    theAttack.hAttackSpeed=1.5
    theAttack.vAttackSpeed=3
    theAttack.absorbProjectiles=true
    theAttack.destroyProjectiles=true
    theAttack.reflectFrameStart=4
    theAttack.reflectFrameEnd=9
    break
       
case AIRSPECIAL: ActionScript(global.character[pNum], SPECIAL); break
case AIRSPECIALSIDE: ActionScript(global.character[pNum], SPECIALSIDE); break
case AIRSPECIALUP: ActionScript(global.character[pNum], SPECIALUP); break
case AIRSPECIALDOWN: ActionScript(global.character[pNum], SPECIALDOWN); break
    
case SPECIALSMASHSIDE: ActionScript(global.character[pNum], SPECIALSIDE); break
case SPECIALSMASHUP: ActionScript(global.character[pNum], SPECIALUP); break
case SPECIALSMASHDOWN: ActionScript(global.character[pNum], SPECIALDOWN); break
case ROLL: SetAttackBoxParameters(10.5); break
case AIRDODGE: SetAttackBoxParameters(15.5,3.5); break
case INITCHAR:
    sprXScale=0.8
    sprYScale=0.8
    gender='m'
    airJumpSpeed=AIRJUMPSPEED-1.5
    maxRunSpeed=MAXRUNSPEED*1.05
    maxAirSpeed=MAXAIRSPEED*1.05
    airCrouchSpeed=-AIRCROUCHSPEED*0.3
    airSlowFallSpeed=-AIRSLOWFALLSPEED*0.3
    attackMultiplier=1.03
    hHitDistanceEquation=HHITDISTANCEEQUATION*0.9
    stunEquation=0.7

    soundWhiff=  'Mr. Game & Watch Beep.wav'
    soundDies=   'Mr. Game & Watch Beep.wav'
    soundSpecial='Mr. Game & Watch Beep.wav'
    break
}